#!/usr/local/bin/gnuplot -p

#
# plots dispersion curves generated with gen_disp.gpl
# @author Tobias Weber <tweber@ill.fr>
# @date 10-february-2023
# @license GPLv2 (see 'LICENSE' file)
#

qrange_M      = 0.0282917/2 # M range
qrange_K      = 0.016332449 # K range
Emrange       = -0.25       # -E range
Erange        = 0.25        # +E range
scale_weight  = 1.5         # weight scale for plot
key_pointsize = 2.0         # symbol size in legend

show_pol      = 1
show_key      = 0           # show legend


a      = 4.558              # lattice constant
#kh_A   = 0.036             # at 20 K
kh_A   = 0.039              # at 29 K
kh_rlu = kh_A / (2*pi / a)

Bc2    = 0.3223
g      = 2.
muB    = 5.78838e-02        # codata value


file_GM = "GM.dat"
file_MK = "MK.dat"
file_KG = "KG.dat"


if(!exists("out_term")) {
	out_term = 1        # 1: pdf, 2: png, others: screen
}

if(out_term == 1) {
	set term pdf color enhanced font "NimbusSans-Regular, 50" size 20, 15
}
if(out_term == 2) {
	set term pngcairo enhanced font "NimbusSans-Regular, 56" size 2666, 2000
	scale_weight = 2.0 * scale_weight
	key_pointsize = 2.0 * key_pointsize
}

set clip points
set border lw 8


# output plot file
if(!exists("file_out")) {
	if(out_term == 1) {
		file_out = "disp.pdf"
	}
	if(out_term == 2) {
		file_out = "disp.png"
	}
}

if(exists("file_out")) {
	set output file_out
	print(sprintf("Plotting to %s.", file_out))
}


# colours
col_axis  = "#000000"  # colour for coordinate cross
col_label = "#000000"  # colour for label
col_scan  = "#aaaaaa"  # colour for scan marker
col_full  = "#999999"  # colour for all weights
col_pol1  = "#0000bb"  # colour for spin-flip channel 1
col_pol2  = "#bb0000"  # colour for spin-flip channel 2
col_pol3  = "#009900"  # colour for non-spin-flip channel


# weight factor
weight_raw(w)         = (w > 5.) ? 5. : sqrt(w) * 2.5
weight(w)             = (weight_raw(w) < 0.04) ? 0 : weight_raw(w)
weight_ifminusE(w, E) = (E < 0) ? weight(w) : 0


E_sign = 1.

$dummy_data << END_DATA
	NaN NaN
END_DATA


set multiplot layout 1, 3


# G -> M branch

set lmargin at screen 0.1
set rmargin at screen 0.39

# ranges
set xrange  [ 0                     : qrange_M             ]
set x2range [ 0/kh_rlu              : qrange_M/kh_rlu      ]
set yrange  [ Emrange               : Erange               ]
set y2range [ Emrange / (g*muB*Bc2) : Erange / (g*muB*Bc2) ]


# ticks
set xtics 0.01 nomirror
set x2tics 0.5
set mxtics 5
set mx2tics 5

set ytics 0.1 nomirror
set y2tics 5
set mytics 5
set my2tics 5


# labels
set xlabel "{/NimbusSans-Italic q} (rlu), G -> M" offset 0, 0.25
set x2label "{/NimbusSans-Italic q} (k_h)" offset 0, -0.4
set ylabel "{/NimbusSans-Italic E} (meV)" offset 2, 0
unset y2label
set format y "%g"
set format y2 ""


# legend
if(show_key != 0) {
        set key at graph 0.49, graph 0.97 height 0.3 samplen 0 Left reverse box opaque
}
else {
        unset key
}


# show coordinate cross
set arrow 1 from 0,Emrange to 0,Erange lw 2 dt 2 lc rgb col_axis nohead
set arrow 2 from 0,0 to qrange_M,0 lw 2 dt 2 lc rgb col_axis nohead


if(show_pol != 0) {
	# polarised results
	plot \
		file_GM using (abs($9)):(E_sign*$4) \
			with points pt 0        lc rgb col_full notitle, \
		"$dummy_data" with points pt 7 ps key_pointsize lc rgb col_pol2 title "SF (+-)", \
		"$dummy_data" with points pt 7 ps key_pointsize lc rgb col_pol1 title "SF (-+)", \
		"$dummy_data" with points pt 7 ps key_pointsize lc rgb col_pol3 title "NSF", \
		file_GM using (abs($9)):(E_sign*$4):(weight($7)*scale_weight) \
			with points pt 7 ps var lc rgb col_pol2 notitle, \
		file_GM using (abs($9)):(E_sign*$4):(weight($6)*scale_weight) \
			with points pt 7 ps var lc rgb col_pol1 notitle, \
		file_GM using (abs($9)):(E_sign*$4):(weight_ifminusE($7, $4)*scale_weight) \
			with points pt 7 ps var lc rgb col_pol2 notitle, \
		file_GM using (abs($9)):(E_sign*$4):(weight($8)*scale_weight) \
			with points pt 7 ps var lc rgb col_pol3 notitle
}
else {
	# unpolarised results
	plot \
	        file_GM using (abs($9)):(E_sign*$4) \
			with points pt 0        lc rgb col_full notitle, \
	        "$dummy_data" with points pt 7 ps key_pointsize lc rgb col_pol2 title "Unpol.", \
	        file_GM using (abs($9)):(E_sign*$4):(weight($6+$7+$8)*scale_weight) \
			with points pt 7 ps var lc rgb col_pol2 notitle
}



# --------------------------------------------------------------------------------



# M -> K branch

set lmargin at screen 0.4
set rmargin at screen 0.6

# ranges
set xrange  [ qrange_M              : qrange_K             ]
set x2range [ qrange_M/kh_rlu       : qrange_M/kh_rlu      ]
set yrange  [ Emrange               : Erange               ]
set y2range [ Emrange / (g*muB*Bc2) : Erange / (g*muB*Bc2) ]


# ticks
set xtics 0.01 nomirror
set x2tics 0.5
set mxtics 5
set mx2tics 5

set ytics 0.1 nomirror
set y2tics 5
set mytics 5
set my2tics 5


# labels
set xlabel "{/NimbusSans-Italic q} (rlu), M -> K" offset 0, 0.25
set x2label "{/NimbusSans-Italic q} (k_h)" offset 0, -0.4
unset ylabel
unset y2label
set format y ""
set format y2 ""


# legend
if(show_key != 0) {
        set key at graph 0.49, graph 0.97 height 0.3 samplen 0 Left reverse box opaque
}
else {
        unset key
}


# show coordinate cross
set arrow 1 from 0,Emrange to 0,Erange lw 2 dt 2 lc rgb col_axis nohead
set arrow 2 from qrange_M,0 to qrange_K,0 lw 2 dt 2 lc rgb col_axis nohead


if(show_pol != 0) {
	# polarised results
	plot \
		file_MK using (abs($9)):(E_sign*$4) \
			with points pt 0        lc rgb col_full notitle, \
		"$dummy_data" with points pt 7 ps key_pointsize lc rgb col_pol2 title "SF (+-)", \
		"$dummy_data" with points pt 7 ps key_pointsize lc rgb col_pol1 title "SF (-+)", \
		"$dummy_data" with points pt 7 ps key_pointsize lc rgb col_pol3 title "NSF", \
		file_MK using (abs($9)):(E_sign*$4):(weight($7)*scale_weight) \
			with points pt 7 ps var lc rgb col_pol2 notitle, \
		file_MK using (abs($9)):(E_sign*$4):(weight($6)*scale_weight) \
			with points pt 7 ps var lc rgb col_pol1 notitle, \
		file_MK using (abs($9)):(E_sign*$4):(weight_ifminusE($7, $4)*scale_weight) \
			with points pt 7 ps var lc rgb col_pol2 notitle, \
		file_MK using (abs($9)):(E_sign*$4):(weight($8)*scale_weight) \
			with points pt 7 ps var lc rgb col_pol3 notitle
}
else {
	# unpolarised results
	plot \
	        file_MK using (abs($9)):(E_sign*$4) \
			with points pt 0        lc rgb col_full notitle, \
	        "$dummy_data" with points pt 7 ps key_pointsize lc rgb col_pol2 title "Unpol.", \
	        file_MK using (abs($9)):(E_sign*$4):(weight($6+$7+$8)*scale_weight) \
			with points pt 7 ps var lc rgb col_pol2 notitle
}



# --------------------------------------------------------------------------------



# K -> G branch

set lmargin at screen 0.61
set rmargin at screen 0.9

# ranges
set xrange  [ qrange_K              : 0                    ]
set x2range [ qrange_K/kh_rlu       : 0/kh_rlu             ]
set yrange  [ Emrange               : Erange               ]
set y2range [ Emrange / (g*muB*Bc2) : Erange / (g*muB*Bc2) ]


# ticks
set xtics 0.01 nomirror
set x2tics 0.5
set mxtics 5
set mx2tics 5

set ytics 0.1 nomirror
set y2tics 5
set mytics 5
set my2tics 5


# labels
set xlabel "{/NimbusSans-Italic q} (rlu), K -> G" offset 0, 0.25
set x2label "{/NimbusSans-Italic q} (k_h)" offset 0, -0.4
unset ylabel
set y2label \
	"{/NimbusSans-Italic E} ({/NimbusSans-Italic g} {/NimbusSans-Italic μ_0} {/NimbusSans-Italic μ_B} {/NimbusSans-Italic H}_{c2}^{int})" \
	offset -0.25, 0
set format y ""
set format y2 "%g"


# legend
if(show_key != 0) {
        set key at graph 0.49, graph 0.97 height 0.3 samplen 0 Left reverse box opaque
}
else {
        unset key
}


# show coordinate cross
set arrow 1 from 0,Emrange to 0,Erange lw 2 dt 2 lc rgb col_axis nohead
set arrow 2 from qrange_K,0 to 0,0 lw 2 dt 2 lc rgb col_axis nohead


if(show_pol != 0) {
	# polarised results
	plot \
		file_KG using (abs($9)):(E_sign*$4) \
			with points pt 0        lc rgb col_full notitle, \
		"$dummy_data" with points pt 7 ps key_pointsize lc rgb col_pol2 title "SF (+-)", \
		"$dummy_data" with points pt 7 ps key_pointsize lc rgb col_pol1 title "SF (-+)", \
		"$dummy_data" with points pt 7 ps key_pointsize lc rgb col_pol3 title "NSF", \
		file_KG using (abs($9)):(E_sign*$4):(weight($7)*scale_weight) \
			with points pt 7 ps var lc rgb col_pol2 notitle, \
		file_KG using (abs($9)):(E_sign*$4):(weight($6)*scale_weight) \
			with points pt 7 ps var lc rgb col_pol1 notitle, \
		file_KG using (abs($9)):(E_sign*$4):(weight_ifminusE($7, $4)*scale_weight) \
			with points pt 7 ps var lc rgb col_pol2 notitle, \
		file_KG using (abs($9)):(E_sign*$4):(weight($8)*scale_weight) \
			with points pt 7 ps var lc rgb col_pol3 notitle
}
else {
	# unpolarised results
	plot \
	        file_KG using (abs($9)):(E_sign*$4) \
			with points pt 0        lc rgb col_full notitle, \
	        "$dummy_data" with points pt 7 ps key_pointsize lc rgb col_pol2 title "Unpol.", \
	        file_KG using (abs($9)):(E_sign*$4):(weight($6+$7+$8)*scale_weight) \
			with points pt 7 ps var lc rgb col_pol2 notitle
}
